#!/usr/local/bin/fish

source script/helpers.fish

argparse 's/skip-install' -- $argv

print_heading "Checking for Homebrew installation"

if not type -q brew
	print_heading "Homebrew is not installed. Installing"
	/bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"
else
	brew -v
end

if not set -q _flag_skip_install
	print_heading "Installing Homebrew packages"

	set homebrew_packages \
		fish \
		wezterm \
		cormacrelf/tap/dark-notify

	for i in $homebrew_packages
		brew install $i
	end

	print_heading "Installing Homebrew fonts"

	set homebrew_fonts \
		font-fira-code \
		font-fira-mono \
		font-fira-sans \
		font-fira-mono-nerd-font \
		font-fantasque-sans-mono \
		font-fantasque-sans-mono-nerd-font


	brew tap homebrew/cask-fonts
	for i in $homebrew_fonts
		brew install --cask $i
	end

	print_heading "Installing Homebrew casks"

	set homebrew_casks \
		wezterm \
		hammerspoon

	for i in $homebrew_casks
		brew install --cask $i
	end
end

print_heading "Checking for Fisher installation"

if not type -q fisher
	print_heading "Fisher is not installed. Installing"
	curl -sL https://git.io/fisher | source && fisher install jorgebucaran/fisher
else
	fisher -v
end

if not set -q _flag_skip_install
	print_heading "Installing Fisher packages"

	set fisher_packages \
		jethrokuan/z \
		jethrokuan/fzf \
		jhillyerd/plugin-git \
		edc/bass

	for i in $fisher_packages
		fisher install $i
	end
end

print_heading "Checking for Rust/Cargo installation"

if not type -q rustc
	print_heading "Rust is not installed. Installing"
	curl --proto '=https' --tlsv1.2 -sSf https://sh.rustup.rs | sh

	print_heading "Adding cargo binaries to path"
	set PATH ~/.cargo/bin $PATH
else
	rustc -V
end

if not type -q sccache
	cargo install sccache &| condense_cargo_output
else
	sccache -V
end

if not set -q _flag_skip_install
	print_heading "Installing Cargo packages"

	set cargo_packages \
		cargo-info \
		irust \
		bacon \
		fd-find \
		ripgrep \
		bat \
		exa \
		rtx-cli \
		gitui \
		bob-nvim \
		wiki-tui \
		starship \
		porsmo

	for i in $cargo_packages
		cargo install $i &| condense_cargo_output
	end
end


print_heading "Symlinking Fish config"

ln -vsfh $PWD/fish/config.fish ~/.config/fish/config.fish
ln -vsfh $PWD/fish/functions/**.fish ~/.config/fish/functions/

print_heading "Symlinking Hammerspoon config"

mkdir -p ~/.hammerspoon
ln -vsfh $PWD/hammerspoon/init.lua ~/.hammerspoon/init.lua

print_heading "Symlinking WezTerm config"

mkdir -p ~/.config/wezterm
ln -vsfh $PWD/wezterm/wezterm.lua ~/.config/wezterm/wezterm.lua

print_heading "Symlinking NeoVim config"

mkdir -p ~/.config/nvim/lua/plugins
mkdir -p ~/.config/nvim/after/ftplugin

ln -vsfh $PWD/neovim/init.lua ~/.config/nvim/init.lua
ln -vsfh $PWD/neovim/plugins.lua ~/.config/nvim/lua/plugins.lua
ln -vsfh $PWD/neovim/plugins/*.lua ~/.config/nvim/lua/plugins/
ln -vsfh $PWD/neovim/ftplugin/*lua ~/.config/nvim/after/ftplugin/

echo -e "\nScript completed successfully ðŸŽ‰"

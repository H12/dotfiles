#!/usr/local/bin/fish

source script/helpers.fish

argparse 's/skip-install' -- $argv

print_heading "Checking for Fisher installation"

if not type -q fisher
	print_heading "Fisher is not installed. Installing"
	curl -sL https://git.io/fisher | source && fisher install jorgebucaran/fisher
else
	fisher -v
end

if not set -q _flag_skip_install
	print_heading "Installing Fisher packages"

	set fisher_packages \
		jethrokuan/z \
		jethrokuan/fzf \
		jhillyerd/plugin-git \
		edc/bass \
		catppuccin/fish

	for i in $fisher_packages
		fisher install $i
	end

	yes | fish_config theme save "Catppuccin Frappe"
end

print_heading "Checking for Rust/Cargo installation"

if not type -q rustc
	print_heading "Rust is not installed. Installing"
	curl --proto '=https' --tlsv1.2 -sSf https://sh.rustup.rs | sh

	print_heading "Adding cargo binaries to path"
	set PATH ~/.cargo/bin $PATH
else
	rustc -V
end

if not type -q sccache
	cargo install sccache &| condense_cargo_output
else
	sccache -V
end

if not set -q _flag_skip_install
	print_heading "Installing Cargo packages"

	set cargo_packages \
		cargo-info \
		irust \
		bacon \
		fd-find \
		ripgrep \
		bat \
		exa \
		rtx-cli \
		gitui \
		bob-nvim \
		wiki-tui \
		starship \
		zellij \
		porsmo

	for i in $cargo_packages
		cargo install $i &| condense_cargo_output
	end
end

print_heading "Installing latest stable NeoVim"

bob install stable
bob use stable

print_heading "Symlinking Bat config"

mkdir -p ~/.config/bat/themes

ln -vsfh $PWD/bat/themes/**.tmTheme ~/.config/bat/themes/
ln -vsfh $PWD/bat/config ~/.config/bat/config

print_heading "Symlinking Fish config"

ln -vsfh $PWD/fish/config.fish ~/.config/fish/config.fish
ln -vsfh $PWD/fish/functions/**.fish ~/.config/fish/functions/

print_heading "Symlinking Kitty config"

mkdir -p ~/.config/kitty
ln -vsfh $PWD/kitty/kitty.conf ~/.config/kitty/kitty.conf

print_heading "Symlinking NeoVim config"

mkdir -p ~/.config/nvim/lua/kickstart/plugins
mkdir -p ~/.config/nvim/lua/custom/plugins

ln -vsfh $PWD/neovim/init.lua ~/.config/nvim/init.lua
ln -vsfh $PWD/neovim/lua/kickstart/plugins/**.lua ~/.config/nvim/lua/kickstart/plugins/
ln -vsfh $PWD/neovim/lua/custom/plugins/**.lua ~/.config/nvim/lua/custom/plugins/

print_heading "Symlinking Starship config"

ln -vsfh $PWD/starship/starship.toml ~/.config/starship.toml

print_heading "Symlinking Zellij config"

mkdir -p ~/.config/zellij
ln -vsfh $PWD/zellij/zellij.kdl ~/.config/zellij/zellij.kdl

echo -e "\nScript completed successfully ðŸŽ‰"
